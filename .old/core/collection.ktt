// Collection traits and wrappers

(|
    outOfBounds = (| parent* = self globals error |) .
|) self globals addTraitSlots

(| 
    collection = (| 
        parent* = self clonable .
        mustImplement = (| -- | self universe globals error MustImplement throw ) .
        outOfBounds = (| -- | self universe globals outOfBounds throw ) .

        size = (| -- | self mustImplement ) .
        copyRemoveAll = (| -- | self mustImplement ) .
        each = (| -- | self mustImplement ) .

        map = (| mapping -- new | self copyRemoveAll self mapInto ) .
        mapInto = (| mapping . into -- new | [ add ] curry compose each ) .
    |) .
|) self std traits addTraitSlots

(|
    indexableCollection = (|
        parent* = self std traits collection .
        size = (| -- size | self mustImplement ) .
        nthUnchecked = (| i -- e | self mustImplement ) .

        bounds = (| -- from . to | 0 self size ) .
        bounds? = (| i -- ? | [ 0 >= ] [ self size < ] bi && ) .

        nth = (| i -- e |
            dup self bounds? [ self nthUnchecked ] [ drop self outOfBounds ] if ) .

        ?nth = (| i -- e/f | dup self bounds? [ self nthUnchecked ] [ drop f ] if ) .

        each = (| q -- |
            [ self nthUnchecked ] swap compose [ self bounds ] dip each[a..b) ) .
    |) .
|) self std traits addTraitSlots

(|
    mutableIndexableCollection = (|
        parent* = self std traits indexableCollection .
        setNthUnchecked = (| e . i -- | self mustImplement ) .
        copyRemoveAll = (| -- | self mustImplement ) .

        setNth = (| e . i -- | 
            dup self bounds? [ self setNthUnchecked ] [ 2drop self outOfBounds ] if ) .

        ?setNth = (| e . i -- | 
            dup self bounds? [ self setNthUnchecked ] [ 2drop ] if ) .

        map = (| mapping -- new | self copyRemoveAll self mapInto ) .

        mapInto = (| mapping . into -- new |  
            [ 
                [ [ self nthUnchecked ] swap compose [ keep ] curry ] dip
                [ setNthUnchecked ] curry compose 
                [ self bounds ] dip each[a..b)
            ] keep ) .
   
        mutateEach = (| mapping -- |
            [ self nthUnchecked ] swap compose
            [ keep self setNthUnchecked ] curry 
            [ self bounds ] dip each[a..b) ) .
    |) .
|) self std traits addTraitSlots

(|
    collection* = self std traits collection .
    indexable* = self std traits mutableIndexableCollection .
|) self std traits bytearray addTraitSlots

(|
    collection* = self std traits collection .
    indexable* = self std traits indexableCollection .
|) self std traits string addTraitSlots

(|
    collection* = self std traits collection .
    indexable* = self std traits mutableIndexableCollection .
|) self std traits array addTraitSlots
