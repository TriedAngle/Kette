// [ val -- ] 
// [ val -- val ] 
(| collection = (| 
    parent* = std traits clonable .
    size = f /* TODO: unimplemented error */ .
    copyRemoveAll = f /* TODO: unimplemented error */ .
    each = f /* TODO: unimplemented error */ .

    map = (| mapping -- new | 
        self copyRemoveAll self mapInto ) .
    mapInto = (| mapping . into -- new | 
        [ add ] curry compose each ) .
|) |) std traits addTraitSlots

(| indexableCollection = (|
    size = (| -- size | childMustImplement! ) .
    nthUnchecked = (| n -- e | childMustImplement! ) . 

    bounds = (| -- from . to | 0 self size ) .  
    bounds? = (| i -- ? | [ 0 >= ] [ self size < ] bi && ) .

    nth = (| i -- e | 
        dup self bounds? [ self nthUnchecked ] [ drop "out of bounds" throw ] if ) .

    ?nth = (| i -- e/f | dup self bounds? [ self nthUnchecked ] [ drop f ] if ) .

    each = (| q -- | 
        [ self nthUnchecked ] swap compose [ self bounds ] dip each[a..b) ) . 

|) |) std traits addTraitSlots

(| 
    printStack = (| -- | "stack: " print @vm-depth >string print "" println ) .
    printStackMsg = (| msg -- | 
        [ "stack: " print @vm-depth >string print " => " print ] dip print "" println ) .
|) universe addTraitSlots

(| mutableIndexableCollection = (|
    indexable* = std traits indexableCollection .
    setNthUnchecked = (| e . n -- | childMustImplement! ) . 
    copyRemoveAll = (| -- new | childMustImplement! ) .

    setNth = (| e . i -- | 
            dup self bounds? [ self setNthUnchecked ] [ 2drop "out of bounds" throw ] if ) .

    ?setNth = (| e . i -- | 
        dup self bounds? [ self setNthUnchecked ] [ 2drop ] if ) .

    map = (| mapping -- new | self copyRemoveAll self mapInto ) .

    mapInto = (| mapping . into -- new |  
        [ 
            [ [ self nthUnchecked ] swap compose [ keep ] curry ] dip
            [ setNthUnchecked ] curry compose 
            [ self bounds ] dip each[a..b)
        ] keep ) .
   
    mutateEach = (| mapping -- |
        [ self nthUnchecked ] swap compose
        [ keep self setNthUnchecked ] curry 
        [ self bounds ] dip each[a..b) ) .
|) |) std traits addTraitSlots

(| 
    indexable* = std traits mutableIndexableCollection .
    collection* = std traits collection .
|) std traits array addTraitSlots

// [ 2 * ] { 1 2 3 4 } map [ >string println ] swap each



