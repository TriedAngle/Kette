@: \s @read-next (array-resize-push) ;
@: s" \s " @skip-whitespace @read-until (array-resize-push) ;

@: [ s" ]" @parse-until array>quotation (array-resize-push) ;

@: { s" }" @parse-until (array-resize-push) ;

@: : 
    @read-next s" )" @read-until drop
    f f s" ;" @parse-until array>quotation 
    9 (special) (new-boa) f namestack|insert 2drop ;

: CHAR_NEWLINE ( -- char ) 2 <bytearray> dup 10 swap 0 set-u8 ;

@: // CHAR_NEWLINE @read-until drop ;
@: /* s" */" @read-until drop ;

// map-slot: name kind value default
// map: name data-slots slots prototype
// array: length capacity <dynamic tagged>
// bytearray: size <dynamic u8>
// quotation: effect body
// word: name effect tags body
// handler: type quotation frame

// TODO: optimize this ;
// x [ ... ] swap >r call r>
// x r> [ ... ] call r>
// x >r ... r>
: dip ( x q -- x ) swap >r (call) r> ;
// x [ ... ] over [ call ] dip
// x [ ... ] x [ call ] dip
// x dup >r [ ... ] call r>
// x dup >r ... r>
: keep ( x q -- x ) over [ (call) ] dip ;
// x [ ..1 ] [ ..2 ] bi
// x dup >r [ ..1 ] call r> [ ..2 ] call
// x dup r> ..1 r> ..2
: bi ( x p q -- ) [ keep ] dip (call) ;
: bi* ( x y p q -- ) [ dip ] dip (call) ;
: bi@ ( x y p -- ) dup bi* ;

: array-index ( n -- idx ) 2 fixnum+ ;
: array-nth ( array n -- ) array-index slot ;
: array-set-nth ( x array n -- ) array-index set-slot ;
: (1array) ( x arr -- arr ) [ 0 array-set-nth ] keep [ 1 swap 0 set-slot ] keep ;
: (2array) ( x y arr -- arr ) [ 1 array-set-nth ] keep [ (1array) 2 swap 0 set-slot ] keep ;
: (3array) ( x y z arr -- arr ) [ 2 array-set-nth ] keep [  (2array) 3 swap 0 set-slot ] keep ;
: (4array) ( x y z w arr -- arr ) [ 3 array-set-nth ] keep [ (3array) 4 swap 0 set-slot ] keep ;
: 1array ( x -- arr ) 1 <array> (1array) ;
: 2array ( x y -- arr ) 2 <array> (2array) ;
: 3array ( x y z -- arr ) 3 <array> (3array) ;
: 4array ( x y z w -- arr ) 4 <array> (4array) ;

: word|tags-null? ( word -- ? ) 2 slot [ f ] [ t ] if ;
: word|init-tags ( word -- ) 10 <array> swap 2 set-slot ; 
: word|?init-tags ( word -- ) dup word|tags-null? [ word|init-tags ] [ drop ] if ;
: word|body ( word -- body ) 3 slot ;
: word|set-body ( body word -- ) 3 set-slot ;
: word|add-tag ( word tag -- ) 
    swap [ word|?init-tags ] keep 
    2 slot swap (array-resize-push) drop ;

: word|map ( -- map ) 9 (special) ;
: box|map ( -- map ) 11 (special) ;
: fixnum|map ( -- map ) 4 (special) ;

: parser-tag ( -- tag ) 13 (special) ;
: inline-tag ( -- tag ) 14 (special) ;

: new-empty-word ( name -- word ) f 10 <array> f word|map (new-boa) ;

: define-word ( name -- word ) 
    new-empty-word [ f namestack|insert 2drop ] keep ;

: scan-new-word ( -- word ) @read-next define-word ;

: ignore-stack-effect ( -- ) s" )" @read-until drop ;
: parse-word-body ( -- quot ) s" ;" @parse-until array>quotation ;


: >box ( value -- box ) box|map (new-boa) ;
: unbox ( box -- value ) 0 slot ;

: inline-tag ( -- tag ) 14 (special) ;

: make-inline ( word -- ) inline-tag word|add-tag ; 

: define-push-word ( word item -- ) 
    1array array>quotation swap [ word|set-body ] keep make-inline ;

: define-symbol ( word -- ) dup >box define-push-word ;

// TODO: errorhandling ;

@: $: scan-new-word define-symbol ;
: get ( symbol -- value ) unbox namestack|find dropd ;
: set ( value symbol -- ) unbox swap namestack|insert 2drop ;

$: last-word

@: : 
    scan-new-word [ last-word set ] keep ignore-stack-effect 
    parse-word-body swap word|set-body ;

@: inline last-word get make-inline ;

$: TODO-LIST

: ?setup-todos ( -- ) TODO-LIST get [ ] [ 10 <array> TODO-LIST set ] if ; 
@: TODO: 
    s" ;" @read-until ?setup-todos 
    TODO-LIST get swap (array-resize-push) TODO-LIST set ;

TODO: lmaooo ;
: map|map ( -- map ) 0 (special) ; inline
: fixnum|map ( -- map ) 4 (special) ; inline
: array|map ( -- map ) 5 (special) ; inline
: map-slot|map ( -- map ) 7 (special) ; inline

: array? ( obj -- ? ) obj>map array|map ref-eq? ;

: parser-tag ( -- tag ) 13 (special) ;

: 4drop ( x y z w -- ) 3drop drop ; inline

: 2dip ( x y q -- x y ) swap [ dip ] dip ; inline
: 3dip ( x y z q -- x y z ) swap [ 2dip ] dip ; inline
: 4dip ( x y z w q -- x y z w ) swap [ 3dip ] dip ; inline

: 2keep ( ..a x y q -- ..b x y ) [ 2dup ] dip 2dip ; inline
: 3keep ( ..a x y z q -- ..b x y z ) [ 3dup ] dip 3dip ; inline
: 4keep ( ..a x y z w q -- ..b x y z w ) [ 4dup ] dip 4dip ; inline

: 2bi ( x y p q -- ) [ 2keep ] dip (call) ; inline
: 2bi* ( a b x y p q -- ) [ 2dip ] dip (call) ; inline
: 2bi@ ( a b x y p -- ) dup 2bi* ; inline

: tri ( x p q r -- ) [ [ keep ] dip keep ] dip (call) ; inline
: tri* ( x y z p q r -- ) [ [ 2dip ] dip dip ] dip (call) ; inline
: tri@ ( x y z p -- ) dup dup tri* ; inline

: rotd ( x y z w -- y z x w ) [ rot ] dip ; inline
: -rotd ( x y z w -- z x y w ) [ -rot ] dip ; inline

: roll ( w x y z -- x y z w ) rotd swap ; inline
: -roll ( x y z w -- w x y z ) swap -rotd ; inline

: 2swap ( a b x y -- x y a b ) roll roll ; inline

: parse-word? ( name -- word/f ) 
    namestack|find swap dup [ dropd ] [ 2drop f ] if ;

: parse-next? ( -- int/word/f ) 
    @read-next dup @parse-int 
    dup [ dropd ] [ parse-word? ] if ;

@: \ @read-next parse-word? 
    dup [ >box (array-resize-push) ] [ drop s" word not found" panic ] if ;

: is-parsing? ( word -- ? ) parser-tag has-tag? ;
: is-word? ( obj -- ? ) obj>map word|map ref-eq? ; 
    
: scan-next ( sss  -- int/word/exec/f ) 
    parse-next? ;

: + ( a b -- c ) fixnum+ ; inline
: - ( a b -- c ) fixnum- ; inline
: * ( a b -- c ) fixnum* ; inline
: / ( a b -- c ) fixnum/ ; inline
: = ( a b -- c ) fixnum= ; inline
: != ( a b -- c ) fixnum!= ; inline
: > ( a b -- c ) fixnum> ; inline
: < ( a b -- c ) fixnum< ; inline
: >= ( a b -- c ) fixnum>= ; inline
: <= ( a b -- c ) fixnum<= ; inline


: merge|size&offset ( src dst -- size offset ) 
    swap [ swap [ 0 slot ] bi@ fixnum+ ] keep 0 slot ;

: bytearray-copy-full ( src dst dst-offset -- ) 
    pick 0 slot 0 -rot copy-bytearray ;

: bytearray<|> ( b1 b2 -- b1b2 ) 
    2dup merge|size&offset [ swapd resize-bytearray ] dip
    swap [ swap bytearray-copy-full ] keep ;

// takes null terminator into account
: bytearray-utf8<|> ( s1 s2 -- s1s2 ) 
    2dup merge|size&offset [ 1 - ] bi@ [ swapd resize-bytearray ] dip
    swap [ swap bytearray-copy-full ] keep ;

: array-copy-full ( src dst dst-offset -- ) pick 0 slot 0 -rot copy-array ;

: array<|> ( a1 a2 -- a1a2 ) 
    2dup merge|size&offset [ 
        swapd [ resize-array dup ] [ swap 0 set-slot ] bi 
    ] dip swap [ swap array-copy-full ] keep ;

TODO: add inline tco ;
: loop ( ... p -- ... )
    [ (call) ] keep swap [ loop ] [ drop ] if ; 

TODO: add inline tco ;
: while ( ... p q -- ... )
    [ [ (call) ] keep ] dip rot
    [ swap [ [ (call) ] keep ] dip swap while ] [ 2drop ] if ;

: map-apply ( q src idx -- mapped ) 
    array-nth swap (call) ; inline

: map ( ..a arr q -- ..a new ) 
    swap dup [ dup 0 slot resize-array ] [ 0 slot ] bi 
    0 swap [ 2dup < ] [ [ [ 
        [ swap [ map-apply ] dip ] keep array-set-nth
    ] 4keep 1 + ] dip ] while rot [ 4drop ] dip ; inline

: enumerate ( arr -- arr ) 
    dup [ dup 0 slot resize-array ] [ 0 slot ] bi 
    0 swap [ 2dup < ] [ [ [ [ 
            [ swapd array-nth ] keep 2array 
        ] keep swapd array-set-nth
    ] 3keep 1 fixnum+ ] dip ] while roll 3drop ; inline

: each-apply ( q arr idx -- ) 
    array-nth swap (call) ; inline

: each ( ..a arr q -- )
    swap dup 0 slot 0 swap [ 2dup < ] [ [
        [ each-apply ] 3keep 1 + ] dip
    ] while 4drop ; inline


: zip-apply ( arr1 arr2 idx -- zipped ) 
    dup swapd [ array-nth ] 2bi@ 2array ;

: zip ( arr1 arr2 -- new )
    2dup [ 0 slot ] bi@ fixnum-min 
    2dup resize-array 0 rot [ 2dup < ] [ [ [
        [ swap [ zip-apply ] dip ] keep array-set-nth
    ] 4keep 1 fixnum+ ] dip ] while 2drop [ 2drop ] dip ;

: first2 ( arr -- first second ) [ 0 array-nth ] [ 1 array-nth ] bi ; inline
: first3 ( arr -- first second third )
    [ 0 array-nth ] [ 1 array-nth ] [ 2 array-nth ] tri ; inline


{   \ dip \ keep \ bi \ bi* \ bi@ \ >box \ unbox \ get \ set
    \ array-index \ array-nth \ array-set-nth 
    \ (1array) \ (2array) \ (3array) \ (4array) 
    \ 1array  \ 2array \ 3array \ 4array
    \ CHAR_NEWLINE
    \ new-empty-word \ define-word \ scan-new-word \ ignore-stack-effect
    \ scan-new-word \ parse-word-body \ inline-tag \ make-inline
    \ define-push-word \ define-symbol
} dup [ unbox make-inline ] each [ unbox @force-recompile ] each

TODO: inspect why this is necessary ;
{ \ : \ $: \ // \ TODO: \ /* } [ unbox @force-recompile ] each

: new-const-data-slot ( name value default -- map-slot ) 
    0 -rot map-slot|map (new-boa) ; 

: new-data-slot ( name value default -- map-slot ) 
    1 -rot map-slot|map (new-boa) ; 

: new-method-slot ( method -- map-slot )
    [ 0 slot 2 ] keep f map-slot|map (new-boa) ;

: new-basic-data-slot ( name idx -- slot ) f new-data-slot ;


// read accessors

TODO: effect ;
: read-accessor-name ( name -- accessor-name ) s" >>" bytearray-utf8<|> ;

: read-accessor-method-body ( idx -- body ) 
    \ self unbox swap \ slot unbox 3array array>quotation ;

: new-read-accessor-method ( name idx -- method )
    [ f 10 <array> ] dip read-accessor-method-body 
    word|map (new-boa) ;

: new-read-accessor-word ( name -- word )
    new-empty-word 
    dup >box \ unbox unbox \ send-self unbox 3array
    array>quotation swap [ 3 set-slot ] keep ;

: new-read-accessor ( name idx -- method word ) 
    [ read-accessor-name ] dip 
    [ new-read-accessor-method ] [ drop new-read-accessor-word ] 2bi ;

: define-read-accessor ( name idx -- method-slot ) 
    new-read-accessor [ new-method-slot ] dip f namestack|insert 2drop ;

: define-read-accessors ( data-slots -- read-slots )
    [ [ 0 slot ] [ 2 slot ] bi define-read-accessor ] map ;

// write accessors

TODO: effect ;
: write-accessor-name (name -- accessor-name ) s" <<" bytearray-utf8<|> ;

: write-accessor-method-body ( idx -- body )
    \ self unbox swap \ set-slot unbox 3array array>quotation ;

: new-write-accessor-method ( name idx -- method ) 
    [ f 10 <array> ] dip write-accessor-method-body
    word|map (new-boa) ;

: new-write-accessor-word ( name -- word )
    new-empty-word
    dup >box \ unbox unbox \ send-self unbox 3array
    array>quotation swap [ 3 set-slot ] keep ;

: new-write-accessor ( name idx -- method word )
    [ write-accessor-name ] dip
    [ new-write-accessor-method ] [ drop new-write-accessor-word ] 2bi ;

: define-write-helper ( name word -- ) 
    [ s" >>" swap bytearray-utf8<|> new-empty-word ] dip
    \ over unbox swap 2array array>quotation 
    swap [ 3 set-slot ] keep f namestack|insert 2drop ;

: define-write-accessor ( name idx -- method-slot ) 
    swap [ swap new-write-accessor ] keep over define-write-helper
    [ new-method-slot ] dip f namestack|insert 2drop ;

: define-write-accessors ( data-slots -- write-slots )
    [ [ 0 slot ] [ 2 slot ] bi define-write-accessor ] map ;

TODO: implement accessor methods and functions ;
: parse-basic-slots ( -- arr )  
    10 <array> @read-next dup [ \s ; bytearray= not ] [
        dupd (array-resize-push) drop
        @read-next dup
    ] while drop ;

: new-empty-map ( name -- map ) f f f map|map (new-boa) ; inline

: print-wall ( -- ) s" -----" (println) ;

: println ( str -- ) (println) ;
: print ( str -- ) (print) ;

: body>> ( obj -- val ) 3 slot ;
: value>> (obj -- val ) 2 slot ;

: new-boa ( ... map -- obj ) (new-boa) ;

@: type:
    @read-next [ \s # swap bytearray-utf8<|> define-word ] [ new-empty-map ] bi 
    [ define-push-word ] keep parse-basic-slots
    enumerate [ first2 new-basic-data-slot ] map 
    [ 0 slot over 1 set-slot ]
    [ dup [ define-read-accessors array<|> ] [ define-write-accessors array<|> ] bi ] bi
    swap 2 set-slot ;


type: vec3 x y z ; 

: vec3+ ( v1 v2 -- v1+v2 ) 
    [ obj>array ] bi@ zip [ first2 + ] map #vec3 array>obj ;

10 20 30 #vec3 new-boa 
5 100 33 #vec3 new-boa
vec3+ obj>array

: parse-quotation ( -- quot ) 
    @read-next drop s" ]" @parse-until 
    array>quotation (array-resize-push) ;

@: if: 
    parse-quotation \ (call) unbox (array-resize-push)
    parse-quotation parse-quotation 
    \ if unbox (array-resize-push) ;


